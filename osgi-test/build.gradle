/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

dependencies {
  ext {
    paxExamVersion = '4.11.0'
    felixVersion = '5.6.10'
  }

  testImplementation project(':impl')
  testImplementation project(':xml')
  testImplementation project(':107')
  testImplementation "javax.cache:cache-api:$parent.jcacheVersion"
  testImplementation "org.apache.felix:org.apache.felix.framework:$felixVersion"
  testImplementation ("org.ops4j.pax.exam:pax-exam-junit4:$paxExamVersion") {
    exclude group:'org.slf4j', module:'slf4j-api'
  }

  testRuntimeOnly "org.slf4j:slf4j-simple:$parent.slf4jVersion",
  testRuntimeOnly ("org.ops4j.pax.exam:pax-exam-container-native:$paxExamVersion") {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
  testRuntimeOnly ("org.ops4j.pax.exam:pax-exam-link-mvn:$paxExamVersion") {
    exclude group:'org.slf4j', module:'slf4j-api'
  }
}

configurations.all {
   resolutionStrategy {
     force 'org.ops4j.base:ops4j-base-lang:1.5.0'
     force 'org.ops4j.base:ops4j-base-util-property:1.5.0'
   }
}

sourceSets {
  test {
    // Needed for PaxExam which makes the dynamic bundle load content of a single dir
    // matching the package of the test class
    output.resourcesDir = java.outputDir
  }
}

test {
  systemProperty 'ehcache.osgi.jar', project(':dist').jar.archivePath.getPath()
  systemProperty 'ehcache.osgi.jcache.version', parent.jcacheVersion
  systemProperty 'ehcache.osgi.slf4j.version', parent.slf4jVersion
}.doFirst {
  if (testJava.javaVersion.isJava9Compatible()) throw new StopExecutionException("OSGi Tests Not Working in Java 9")
}

test.dependsOn ':dist:jar'
